import parser

var test_start = clock()
var test_memory = mem()

func print()
{
    for(var i in args)
    {
        Test._out += str(i)
        __std__.print(i)
    }
}

func println()
{
    for(var i in args)
    {
        Test._out += str(i)
        __std__.print(i)
    }
    Test._out += '\n'
    __std__.println()
}

class Test
{
    static var p, out, _out, exprs

    static func expect(out)
    {
        Test.out = str(out)
    }

    static func test(expr, out, name)
    {
        Test.exprs.add([expr, out, name])
    }

    static func run(arguments)
    {
        var execute = false
        if(arguments is list)
        {
            Test.p.parse(arguments)
            execute = Test.p['test']
        }
        else if(arguments is null or arguments == true)
            execute = true
            
        if(execute)
        {
            var success = 0
            var total = 0
            if(Test.out is not null)
            {
                total++
                if(Test.out == Test._out or (len(Test._out) > 0 and Test.out == Test._out.substr(0, len(Test._out)-1)))
                {
                    success++
                    __std__.println("Output: PASS")
                }
                else
                    __std__.println("Output: FAILED")
            }

            var ret, ts, out, name
            for(var t, i in Test.exprs)
            {
                total++

                ts = t[0]
                out = t[1]
                name = t[2]

                if(ts is func)
                    ret = ts()
                else
                    ret = ts
                
                if(ret == out)
                {
                    success++
                    if(name is null)
                        __std__.println("Test ", i, ": PASS")
                    else
                        __std__.println("Test ", i, " [", name,"]: PASS")
                }
                else
                {
                    if(name is null)
                        __std__.println("Test ", i, ": FAILED")
                    else
                        __std__.println("Test ", i, " [", name,"]: FAILED")
                }
            }

            __std__.println("--------------------");
            __std__.println("Total time: ", (clock() - test_start), "s")
            __std__.println("Total memory: ", (mem() - test_memory), " bytes")
            __std__.println("Coverage: ", (success / total) * 100, "%")
        }
    }
}

Test.p = parser.Parser("Unit testing", "Unit testing", 0.1)
Test.p.add('test', 't', null, 0)

Test.out = null
Test._out = ''
Test.exprs = []