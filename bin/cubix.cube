#!/usr/local/bin/cube

cube
{
    title = "Cubix",
    description = "The cube package manager",
    bin = "cubix"
}

import json;
import curl;
import os;
import paths;
import zip;
import parser as default;

var parser = Parser('Cubix', 'The cube package manager', '0.1');
parser.add('install', 'i', str, '+', 'selects the package to install');
parser.add('uninstall', 'u', str, '+', 'selects the package to uninstall');
parser.add('list', 'l', null, 0, 'list installed packages');
parser.add('online', 'o', null, 0, 'list online packages');
parser.add('force', 'f', null, 0, 'force install or uninstall');
parser.add('description', 'd', str, '+', 'gets the description of the package');

parser.parse(args);

var address = 'https://raw.githubusercontent.com/AlexanderSilvaB/cube-packages/master/';

func readWeb(request)
{
    var url = address+request;
    var contents = curl.get(url);
    if(contents is null)
        throw('Failed to get [', url, ']');
    return str(contents);
}

func install(force, libs)
{
    println('Installing: ', libs.join(' '));

    var rm = []
    for(var lib in libs)
    {
        if(lib.endsWith('.zip'))
        {
            installFilePackage(force, lib);
            rm.add(lib)
        }
    }

    for(var lib in rm)
    {
        libs.remove(lib)
    }

    if(len(libs) == 0)
        return;

    var contents = readWeb('index.json');
    var packages = json.parseJson(contents)['packages'];

    var package = null;

    for(var lib in libs)
    {
        if(lib.endsWith('.zip'))
            continue;

        package = null;
        for(var p in packages)
        {
            if(p.endsWith('/{}'.format(lib)))
            {
                package = p;
            }
        }

        if(package is null)
            throw('Package "', lib, '" not found!');

        installPackage(force, lib, package);
    }
}

func uninstall(force, libs)
{
    println('Uninstalling: ', libs.join(' '));

    for(var lib in libs)
    {
        uninstallPackage(force, lib);
    }   
}

func description(libs)
{
    println('Describing: ', libs.join(' '));

    var rm = []
    for(var lib in libs)
    {
        if(lib.endsWith('.zip'))
        {
            descriptionFilePackage(lib);
            rm.add(lib)
        }
    }

    for(var lib in rm)
    {
        libs.remove(lib)
    }

    if(len(libs) == 0)
        return;

    var contents = readWeb('index.json');
    var packages = json.parseJson(contents)['packages'];

    var package = null;

    for(var lib in libs)
    {
        if(lib.endsWith('.zip'))
            continue;

        package = null;
        for(var p in packages)
        {
            if(p.endsWith('/{}'.format(lib)))
            {
                package = p;
            }
        }

        if(package is null)
            throw('Package "', lib, '" not found!');

        descriptionPackage(lib, package);
    }
}

func listPackages()
{
    var files = ls(os.allowedLibsPath);
    var size = 0;
    for(var file in files)
    {
        if(file.endsWith('.json'))
        {
            if(listPackage(file))
                ++size;
        }
    }
    
    if(size == 0)
        println("No packages found");
}

func listOnlinePackages()
{
    var contents = readWeb('index.json');
    var packages = json.parseJson(contents)['packages'];
    for(var p in packages)
    {
        print(p.split('/')[-1], ', ');
    }
    println('\b\b  ');
}

func installFilePackage(force, libFile)
{
    println("File: ", libFile)
    if(!exists(libFile))
    {
        println("Invalid package file.")
        return;
    }
    
    var file = zip.Zip(libFile, 'r')
    var contents = file.read('package.json')
    if(contents is null)
    {
        println("Not a valid cube package.")
        return;
    }

    var package = json.parseJson(str(contents));
    var lib = package['name']
    if(lib is null)
    {
        println("Not a valid cube package.")
        return;
    }

    var current = null;
    try
    {
        var j = json.readJson("{}/{}.json".format(os.allowedLibsPath, lib));
        current = j['version'];
    }

    var version = package['version'];
    if(version is null)
    {
        println("Not a valid cube package.")
        return;
    }

    println("{} [{} -> {}]\t".format(lib, current, version));
    if(not force and current == version)
    {
        println("Already updated");
        return;
    }
    var data;
    var name;
    var path;

    path = os.allowedLibsPath+'/'+lib+'.json';

    with(path, 'w')
    {
        file.write(str(contents));
    }

    path = os.allowedBinPath + '/' + lib;
    if(!exists(path))
        mkdir(path, 777);

    path = os.allowedLibsPath + '/' + lib;
    if(!exists(path))
        mkdir(path, 777);
    
    path = os.allowedSamplesPath + '/' + lib;
    if(!exists(path))
        mkdir(path, 777);

    for(var f in package['files'])
    {
        print('Installing: ', f);
        data = file.read(f);
        if(data is null)
        {
            println('Failed!');
            continue;
        }
        name = paths.fileName(f);
        if(package['bin'] is list and name in package['bin'])
            path = os.allowedBinPath + '/' + lib;
        else   
            path = os.allowedLibsPath + '/' + lib;

        path += '/{}/{}'.format(name);
        
        with(path, 'w')
        {
            file.write(str(data));
        }

        println('\rInstalled: ', f, ' ');
    }

    for(var f in package['samples'])
    {
        print('Installing: ', f);
        data = file.read(f);
        if(data is null)
        {
            println('Failed!');
            continue;
        }
        name = paths.fileName(f);
        path = os.allowedSamplesPath + '/' + lib;

        path += '/{}'.format(name);
        
        with(path, 'w')
        {
            file.write(str(data));
        }

        println('\rInstalled: ', f, ' ');
    }

    for(var f in package['native'])
    {
        print('Installing: ', f);
        data = file.read('native/{}/{}/{}{}'.format(os.name, os.arch, f, os.libsExt));
        if(data is null)
        {
            println('Failed!');
            continue;
        }

        name = paths.fileName(f);
        path = os.allowedLibsPath + '/' + lib;
        path += '/{}{}'.format(name, os.libsExt);
        

        with(path, 'w')
        {
            file.write(str(data));
        }

        println('\rInstalled: ', f, ' ');
    }

}

func installPackage(force, lib, url)
{
    var contents = readWeb('{}/package.json'.format(url));
    var package = json.parseJson(contents);
    
    var current = null;
    try
    {
        var j = json.readJson("{}/{}.json".format(os.allowedLibsPath, lib));
        current = j['version'];
    }

    var version = package['version'];
    println("{} [{} -> {}]\t".format(lib, current, version));
    if(not force and current == version)
    {
        println("Already updated");
        return;
    }
    var data;
    var name;
    var path;

    path = os.allowedLibsPath+'/'+lib+'.json';

    with(path, 'w')
    {
        file.write(contents);
    }

    path = os.allowedBinPath + '/' + lib;
    if(!exists(path))
        mkdir(path, 777);

    path = os.allowedLibsPath + '/' + lib;
    if(!exists(path))
        mkdir(path, 777);
    
    path = os.allowedSamplesPath + '/' + lib;
    if(!exists(path))
        mkdir(path, 777);

    for(var f in package['files'])
    {
        print('Downloading: ', f);
        data = readWeb('{}/{}'.format(url, f));
        name = paths.fileName(f);
        if(package['bin'] is list and name in package['bin'])
            path = os.allowedBinPath + '/' + lib;
        else   
            path = os.allowedLibsPath + '/' + lib;

        path += '/{}'.format(name);
        
        with(path, 'w')
        {
            file.write(data);
        }

        println('\rDownloaded: ', f, ' ');
    }

    for(var f in package['samples'])
    {
        print('Downloading: ', f);
        data = readWeb('{}/{}'.format(url, f));
        name = paths.fileName(f);
           
        path = os.allowedSamplesPath + '/' + lib;

        path += '/{}'.format(name);
        
        with(path, 'w')
        {
            file.write(data);
        }

        println('\rDownloaded: ', f, ' ');
    }

    for(var f in package['native'])
    {
        print('Downloading: ', f);
        data = readWeb('{}/native/{}/{}/{}{}'.format(url, os.name, os.arch, f, os.libsExt));
        name = paths.fileName(f);
        path = os.allowedLibsPath + '/' + lib;
        path += '/{}{}'.format(name, os.libsExt);
        

        with(path, 'w')
        {
            file.write(data);
        }

        println('\rDownloaded: ', f, ' ');
    }
}

func descriptionFilePackage(libFile)
{
    println("File: ", libFile)
    if(!exists(libFile))
    {
        println("Invalid package file.")
        return;
    }
    
    var file = zip.Zip(libFile, 'r')
    var contents = file.read('package.json')
    if(contents is null)
    {
        println("Not a valid cube package.")
        return;
    }

    var package = json.parseJson(str(contents));
    var lib = package['name']
    if(lib is null)
    {
        println("Not a valid cube package.")
        return;
    }

    var current = null;
    try
    {
        var j = json.readJson("{}/{}.json".format(os.allowedLibsPath, lib));
        current = j['version'];
    }

    var version = package['version'];
    if(version is null)
    {
        println("Not a valid cube package.")
        return;
    }

    var desc = package['description'];
    var dependencies = package['dependencies'];

    println('Name: ', lib)
    println('Description: ', desc)
    if(current is not null)
        println('Installed version: ', current)
    println('Available version: ', version)
    if(dependencies is list)
    {
        if(len(dependencies) == 0)
            println('Dependencies: None')
        else
            println('Dependencies: ', dependencies.join(' '))
    }
    
    var name;

    println('Files')
    for(var f in package['files'])
    {
        name = paths.fileName(f);
    
        if(package['bin'] is list and name in package['bin'])
            println(' *', name)
        else   
            println(' ', name)
    }

    println('Samples')
    for(var f in package['samples'])
    {
        name = paths.fileName(f);
        println(' ', name);
    }


    println('Native')
    for(var f in package['native'])
    {
        name = paths.fileName(f);
        println(' ', name);
    }

}

func descriptionPackage(lib, url)
{
    var contents = readWeb('{}/package.json'.format(url));
    var package = json.parseJson(contents);
    
    var current = null;
    try
    {
        var j = json.readJson("{}/{}.json".format(os.allowedLibsPath, lib));
        current = j['version'];
    }

    var version = package['version'];
    var desc = package['description'];
    var dependencies = package['dependencies'];

    println('Name: ', lib)
    println('Description: ', desc)
    if(current is not null)
        println('Installed version: ', current)
    println('Available version: ', version)
    if(dependencies is list)
    {
        if(len(dependencies) == 0)
            println('Dependencies: None')
        else
            println('Dependencies: ', dependencies.join(' '))
    }

    var name;

    println('Files')
    for(var f in package['files'])
    {
        name = paths.fileName(f);
        if(package['bin'] is list and name in package['bin'])
            println(' *', name);
        else   
            println(' ', name);
    }

    println('Samples')
    for(var f in package['samples'])
    {
        name = paths.fileName(f);
        println(' ', name);
    }

    println('Native')
    for(var f in package['native'])
    {
        name = paths.fileName(f);
        println(' ', name);
    }
}

func uninstallPackage(force, lib)
{
    var package = json.readJson("{}/{}.json".format(os.allowedLibsPath, lib));
    if(package == null or len(package) == 0)
    {
        println("Not found!");
        return;
    }

    var name;
    var path;

    for(var f in package['files'])
    {
        print('Removing: ', f);
        name = paths.fileName(f);
        if(package['bin'] is list and name in package['bin'])
            path = os.allowedBinPath + '/' + lib;
        else   
            path = os.allowedLibsPath + '/' + lib;

        path += '/{}'.format(name);
        
        remove(path);

        println('\rRemoved: ', f, ' ');
    }

    for(var f in package['samples'])
    {
        print('Removing: ', f);
        name = paths.fileName(f);

        path = os.allowedSamplesPath + '/' + lib;

        path += '/{}'.format(name);
        
        remove(path);

        println('\rRemoved: ', f, ' ');
    }

    for(var f in package['native'])
    {
        print('Removing: ', f);
        name = paths.fileName(f);
        path = os.allowedLibsPath + '/' + lib;
        path += '/{}{}'.format(name, os.libsExt);
        
        remove(path);

        println('\rRemoved: ', f, ' ');
    }

    path = os.allowedBinPath + '/' + lib;
    remove(path);

    path = os.allowedLibsPath + '/' + lib;
    remove(path);

    path = os.allowedSamplesPath + '/' + lib;
    remove(path);

    path = os.allowedLibsPath+'/'+lib+'.json';
    remove(path);
}

func listPackage(lib)
{
    var path = '{}/{}'.format(os.allowedLibsPath, lib);
    var package = json.readJson(path);
    if(package is null or len(package) == 0)
        return false;

    println(package['name'], ':', package['version'], '  [', package['description'], ']');
    return true;
}

if(!exists(os.allowedBinPath))
    mkdir(os.allowedBinPath, 777)
if(!exists(os.allowedLibsPath))
    mkdir(os.allowedLibsPath, 777)
if(!exists(os.allowedSamplesPath))
    mkdir(os.allowedSamplesPath, 777)

if(parser.args['install'] is not null and len(parser.args['install']) > 0 )
{
    install(parser.args['force'], parser.args['install']);
}
else if( parser.args['uninstall'] is not null and len(parser.args['uninstall']) > 0 )
{
    uninstall(parser.args['force'], parser.args['uninstall']);
}
else if( parser.args['list'])
{
    listPackages();
}
else if( parser.args['online'] )
{
    listOnlinePackages();
}
else if( parser.args['description'] is not null and len(parser.args['description']) > 0 )
{
    description(parser.args['description']);
}
else
{
    parser.header();
}
